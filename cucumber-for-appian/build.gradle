plugins {
    id 'java'
}

group 'com.appiancorp.ps.automatedtest'

// Load common gradle properties
project.ext["repo_root"] = "$rootDir"
def commonPropertiesFile = file("$rootDir/shared/gradle/common_gradle.properties")
if (commonPropertiesFile.exists()) {
    commonPropertiesFile.withReader { reader ->
        def properties = new Properties()
        properties.load(reader)
        properties.each { key, value ->
            project.ext[key] = value
        }
    }
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.11.0'
    testImplementation 'io.cucumber:cucumber-junit:7.21.1'
    testImplementation 'io.cucumber:cucumber-picocontainer:7.21.1'

    implementation 'io.cucumber:cucumber-java:7.21.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.3'
    implementation project(':appian-selenium-api')
}

def finalDestinationDir = file("${buildDir}/RELEASE-FILES")
version = project["version"]
def cucumberForAppianBuildDir = file("${buildDir}/${project.name}-${version}")
apply from: "$rootDir/shared/gradle/commons.gradle"


/*
  Overwriting the default jar task in order to create a fat jar that will contain the
  FitNesseForAppian dependency jar.
 */

jar {
    manifest {
        attributes "Main-Class": "com.appiancorp.ps.automatedtest"
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

/*
  These tasks build the correct directory structure for the TestExample that we ship with the release
 */

task copyOutputJARIntoTestExampleLib(type: Copy) {
    /*
      jar.outputs.files is only valid after the build task runs so the dependence is implicit. But adding an
      explicit dependsOn to make all the tasks consistent and easy to read.
     */
    dependsOn build
    from jar.outputs.files
    into "build/resources/main/TestExample/lib"
}

task copyConfigsIntoTestExample(type: Copy) {
    dependsOn build
    from "${projectDir}/../shared/configs"
    into "build/resources/main/TestExample/src/main/resources/configs"
}

task copyTestExampleIntoFinalReleaseDirectory(type: Copy) {
    dependsOn copyOutputJARIntoTestExampleLib, copyConfigsIntoTestExample
    from "build/resources/main/TestExample"
    into cucumberForAppianBuildDir

    version = project["version"]
    doLast {
        def path = "${cucumberForAppianBuildDir}/pom.xml"
        def updated = file(path).text.replaceAll('CUCUMBER_FOR_APPIAN_VERSION_REPLACEMENT_KEY', version)
        new File(path).text = updated
    }
}

/*
  These tasks are for copying setup scripts and drivers from the base project directory into the current
  release directory
 */

task copyScriptsIntoFinalReleaseDirectory(type: Copy) {
    dependsOn build
    from "${projectDir}/setupCustomPropertiesForMac.sh"
    into cucumberForAppianBuildDir
    from "${projectDir}/setupCustomPropertiesForLinux.sh"
    into cucumberForAppianBuildDir
}

task copyDriversIntoFinalReleaseDirectory(type: Copy) {
    dependsOn build
    from "${projectDir}/../shared/lib/drivers/"
    into "${cucumberForAppianBuildDir}/lib/drivers/"
}

/**
 * ------------------------Release Tasks-----------------------
 */

task setReleaseVersion() {
    validateReleaseNumbers(project)
}

task zipCucumberForAppian(type: Zip) {
    dependsOn clean, build, copyScriptsIntoFinalReleaseDirectory, copyTestExampleIntoFinalReleaseDirectory, copyDriversIntoFinalReleaseDirectory
    from cucumberForAppianBuildDir
    version = project["version"]
    archiveFileName = "${project.name}.zip"
    destinationDirectory = finalDestinationDir
}

/**
 * Execute this task from terminal in this format: ./gradlew releaseCucumberForAppian
 */
task releaseCucumberForAppian(type: Zip) {
    dependsOn setReleaseVersion, clean, build, zipCucumberForAppian
    build.mustRunAfter('clean')
}
