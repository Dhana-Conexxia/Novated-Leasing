
plugins {
    id 'java'
    id 'checkstyle'
    id 'idea'
}

checkstyle {
    toolVersion =  '10.23.0'
}

tasks.withType(JavaCompile).configureEach {
    options.deprecation = true
}

group 'com.appiancorp.ps.automatedtest'

// Load common gradle properties
project.ext["repo_root"] = "$rootDir"
def commonPropertiesFile = file("$rootDir/shared/gradle/common_gradle.properties")
if (commonPropertiesFile.exists()) {
    commonPropertiesFile.withReader { reader ->
        def properties = new Properties()
        properties.load(reader)
        properties.each { key, value ->
            project.ext[key] = value
        }
    }
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.11.0'
    testImplementation 'io.cucumber:cucumber-junit:7.21.1'
    testImplementation 'io.cucumber:cucumber-picocontainer:7.21.1'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.24.3'
    //implementation 'org.slf4j:slf4j-log4j12:2.0.9'
    testImplementation 'tech.grasshopper:extentreports-cucumber7-adapter:1.14.0'
    implementation 'org.seleniumhq.selenium:selenium-java:4.25.0'
    implementation 'org.slf4j:slf4j-simple:2.0.17'

    implementation 'io.cucumber:cucumber-java:7.21.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.3'
    implementation project(':appian-selenium-api')

    // Masterthought cucumber-reporting (for rich HTML reports)
    testImplementation 'net.masterthought:cucumber-reporting:5.8.0'


    // JUnit (if not already added)
    testImplementation 'junit:junit:4.13.2'
}

def finalDestinationDir = file("${buildDir}/RELEASE-FILES")
version = project["version"]
def cucumberForAppianBuildDir = file("${buildDir}/${project.name}-${version}")
apply from: "$rootDir/shared/gradle/commons.gradle"


/*
  Overwriting the default jar task in order to create a fat jar that will contain the
  FitNesseForAppian dependency jar.
 */

jar {
    manifest {
        attributes "Main-Class": "com.appiancorp.ps.automatedtest"
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

/*
  These tasks build the correct directory structure for the TestExample that we ship with the release
 */

task copyOutputJARIntoTestExampleLib(type: Copy) {
    /*
      jar.outputs.files is only valid after the build task runs so the dependence is implicit. But adding an
      explicit dependsOn to make all the tasks consistent and easy to read.
     */
    dependsOn build
    from jar.outputs.files
    into "build/resources/main/TestExample/lib"
}

task copyConfigsIntoTestExample(type: Copy) {
    dependsOn build
    from "${projectDir}/../shared/configs"
    into "build/resources/main/TestExample/src/main/resources/configs"
}

task copyTestExampleIntoFinalReleaseDirectory(type: Copy) {
    dependsOn copyOutputJARIntoTestExampleLib, copyConfigsIntoTestExample
    from "build/resources/main/TestExample"
    into cucumberForAppianBuildDir

    version = project["version"]
    doLast {
        def path = "${cucumberForAppianBuildDir}/pom.xml"
        def updated = file(path).text.replaceAll('CUCUMBER_FOR_APPIAN_VERSION_REPLACEMENT_KEY', version)
        new File(path).text = updated
    }
}

/*
  These tasks are for copying setup scripts and drivers from the base project directory into the current
  release directory
 */

task copyScriptsIntoFinalReleaseDirectory(type: Copy) {
    dependsOn build
    from "${projectDir}/setupCustomPropertiesForMac.sh"
    into cucumberForAppianBuildDir
    from "${projectDir}/setupCustomPropertiesForLinux.sh"
    into cucumberForAppianBuildDir
}

/**
 * ------------------------Release Tasks-----------------------
 */

task setReleaseVersion() {
    validateReleaseNumbers(project)
}

task zipCucumberForAppian(type: Zip) {
    dependsOn clean, build, copyScriptsIntoFinalReleaseDirectory, copyTestExampleIntoFinalReleaseDirectory
    from cucumberForAppianBuildDir
    version = project["version"]
    archiveFileName = "${project.name}.zip"
    destinationDirectory = finalDestinationDir
}

/**
 * Execute this task from terminal in this format: ./gradlew releaseCucumberForAppian
 */
task releaseCucumberForAppian(type: Zip) {
    dependsOn setReleaseVersion, clean, build, zipCucumberForAppian
    build.mustRunAfter('clean')
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

/*task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--plugin', 'junit:results/cucumber-junit-report.xml', '--glue', 'com.appiancorp.ps.cucumber', 'src/test/resources']
        }
    }
}*/


task cucumber(type: JavaExec) {
    dependsOn testClasses
    mainClass.set("io.cucumber.core.cli.Main")
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = [
            '--plugin', 'pretty',
            '--plugin', 'html:build/reports/cucumber/',
            '--plugin', 'json:build/reports/cucumber/cucumber.json',
            '--plugin', 'com.aventstack.extentreports.cucumber.adapter.ExtentCucumberAdapter:',
            '--glue', 'testrunner',
            'src/test/resources/features'
    ]
}

test {
    // Use JUnit 4 to support @RunWith(Cucumber.class)
    useJUnit()

    // Display detailed log output
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        exceptionFormat "full"
    }
    // Generate cucumber JSON report
    systemProperty "cucumber.plugin", "pretty, json:build/reports/cucumber/cucumber.json"
    testLogging {
        events "passed", "failed", "skipped"
    }
    // Cucumber reporting + Extent + JSON
    systemProperty "cucumber.plugin",
            "pretty, html:build/reports/tests/cucumber-html-report, json:build/reports/tests/cucumber.json, com.aventstack.extentreports.cucumber.adapter.ExtentCucumberAdapter:"

    // Ensure reports go to build/reports/ExtendReport (optional)
    systemProperty "extent.reporter.spark.start", "true"
    systemProperty "extent.reporter.spark.out", "build/reports/ExtendReport/ExtentReport.html"
    systemProperty "extent.reporter.spark.config", "src/test/resources/extent-config.xml"
    systemProperty "timestamp", new Date().format("yyyy-MM-dd_HH-mm-ss")
}

tasks.register('generateCucumberReport', JavaExec) {
    dependsOn test
    mainClass = 'util.CucumberReportGenerator'
    classpath = sourceSets.test.runtimeClasspath
}
tasks.test {
    useJUnit()
    // Optional: delete previous reports before running
    doFirst {
        delete fileTree("${buildDir}/reports") {
            include '**/*'
        }
    }
    outputs.upToDateWhen { false } // force tests to always re-run
}